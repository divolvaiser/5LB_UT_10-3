 Перем СоответствиеТиповИПредставлений;
 Перем ТипыДокументов;
 Перем ПараметрыСФ;
 Перем мВалютаРегламентированногоУчета;
 
#Если Клиент Тогда

// Формирование отчета в виде табличного документа.
// Параметры:
//  ТабличныйДокумент - макет отчета.
Процедура СформироватьОтчет(ТабличныйДокумент) Экспорт
	
	МассивПрефиксовДляРИБиОрганизации = ОбщегоНазначения.СформироватьМассивПрефиксовДляРИБИОрганизации(Организация);
	
	ТабличныйДокумент.Очистить();
	ТабличныйДокумент.АвтоМасштаб = Истина;
	Макет = ПолучитьМакет("Макет");

	// Вывод шапки
	Секция = Макет.ПолучитьОбласть("Шапка");
	Секция.Параметры.НачалоПериода = Формат(НачалоПериода, "ДФ=dd.MM.yyyy");
	Секция.Параметры.КонецПериода = Формат(КонецПериода, "ДФ=dd.MM.yyyy");
	Секция.Параметры.НазваниеОрганизации = ?(ЗначениеЗаполнено(Организация.НаименованиеПолное), Организация.НаименованиеПолное, Организация);
	ТабличныйДокумент.Вывести(Секция);
	
	// Выполнение запроса.
	Результат = ПодготовитьОтчетКВыводуНаПечать();
	
	мВалютаРегламентированногоУчета = Константы.ВалютаРегламентированногоУчета.Получить();
	
	Если Результат.Пустой() Тогда
	
		Секция = Макет.ПолучитьОбласть("Строка");
        ТабличныйДокумент.Вывести(Секция);
		УправлениеОтчетами.УстановитьКолонтитулыПоУмолчанию(ТабличныйДокумент, , Строка(глЗначениеПеременной("глТекущийПользователь")));
	    Возврат;
		
	КонецЕсли; 
	
	Секция = Макет.ПолучитьОбласть("Строка");
	Счетчик = 1;
	
	Если Не СформироватьОтчетПоСтандартнойФорме и ГруппироватьПоКонтрагентам Тогда
		//Режим построения с группировкой по контрагенту
		СекцияКонтрагента = Макет.ПолучитьОбласть("Контрагент");
		ГруппировкаПоКонтрагенту = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		ТабличныйДокумент.НачатьАвтогруппировкуСтрок();
		Пока ГруппировкаПоКонтрагенту.Следующий() Цикл
			СекцияКонтрагента.Параметры.Заполнить(ГруппировкаПоКонтрагенту);
			ТабличныйДокумент.Вывести(СекцияКонтрагента,1);
		
			Документ = ГруппировкаПоКонтрагенту.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			Пока Документ.Следующий() Цикл
				Секция.Параметры.Заполнить(Документ);
				Секция.Параметры.НПП = Счетчик;
				Секция.Параметры.КонтрагентНаименование = СокрЛП(Секция.Параметры.КонтрагентНаименование);
				
				Если ВыводСтроки(Документ, Секция, МассивПрефиксовДляРИБиОрганизации) Тогда
					ТабличныйДокумент.Вывести(Секция,2);
					Счетчик = Счетчик + 1;
				КонецЕсли;
				
			КонецЦикла; 
		КонецЦикла; 
		
		ТабличныйДокумент.ЗакончитьАвтогруппировкуСтрок();
	Иначе
		// Простой режим
		Документ = Результат.Выбрать();
		
		Пока Документ.Следующий() Цикл
			Секция.Параметры.Заполнить(Документ);
			Секция.Параметры.НПП = Счетчик;
			Секция.Параметры.КонтрагентНаименование = СокрЛП(Секция.Параметры.КонтрагентНаименование);
			
			Если ВыводСтроки(Документ, Секция, МассивПрефиксовДляРИБиОрганизации) Тогда
				ТабличныйДокумент.Вывести(Секция);
				Счетчик = Счетчик + 1;
			КонецЕсли;
			
		КонецЦикла; 
	
	КонецЕсли;
	
	ТабличныйДокумент.ПовторятьПриПечатиСтроки = ТабличныйДокумент.Области.ШапкаТаблицы;
	
	УправлениеОтчетами.УстановитьКолонтитулыПоУмолчанию(ТабличныйДокумент, , Строка(глЗначениеПеременной("глТекущийПользователь")));
	
КонецПроцедуры // СформироватьОтчет()

Функция ВыводСтроки(Документ, Секция, МассивПрефиксовДляРИБиОрганизации)
	
	ДокументыОснования = Документ.ДокументыОснования.Выбрать();
	
	ПредставлениеОснования = "";
	
	ВывестиОрганизацию = Ложь;
	
	Пока ДокументыОснования.Следующий() Цикл
		
		ТипДокументаОснования = ТипЗнч(ДокументыОснования.ДокументОснование);
		
		Если ПустаяСтрока(ПредставлениеОснования) Тогда
			Секция.Параметры.ДокументОснование = ДокументыОснования.ДокументОснование;
		Иначе
			ПредставлениеОснования = ПредставлениеОснования + Символы.ПС;
		КонецЕсли; 
				
		ПредставлениеТипа = ПолучитьПредставлениеПоТипу(ТипДокументаОснования);
				
		Если Не ПредставлениеТипа = Неопределено Тогда
			ПредставлениеОснования = ПредставлениеОснования + ПредставлениеТипа + " № " + ОбщегоНазначения.ПолучитьНомерНаПечать(ДокументыОснования.ДокументОснование,МассивПрефиксовДляРИБиОрганизации) + " от "+ Формат(ДокументыОснования.Дата, "ДФ=dd.MM.yyyy") + " г.";
		Иначе	
			ПредставлениеОснования = ПредставлениеОснования + Строка(ДокументыОснования.ДокументОснование);
		КонецЕсли; 
		
	КонецЦикла;
	
	Если ПустаяСтрока(ПредставлениеОснования) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если Документ.ОпределитьПараметрыСчетаФактуры Тогда
		
		УчетНДС.ПолучитьПараметрыСчетаФактуры(Документ.СчетФактура, мВалютаРегламентированногоУчета, ПараметрыСФ);
		Секция.Параметры.Сумма = ?(ПараметрыСФ.СуммаДокумента = 0, "", Формат(ПараметрыСФ.СуммаДокумента, "ЧЦ=19; ЧДЦ=2") + " " + Строка(ПараметрыСФ.ВалютаДокумента));
		
		Секция.Параметры.Контрагент = ПараметрыСФ.Контрагент;
		Если ЗначениеЗаполнено(ПараметрыСФ.Контрагент)  Тогда
			Секция.Параметры.КонтрагентНаименование = ?(ПустаяСтрока(ПараметрыСФ.Контрагент.НаименованиеПолное), СокрЛП(ПараметрыСФ.Контрагент), СокрЛП(ПараметрыСФ.Контрагент.НаименованиеПолное));
		КонецЕсли; 
				
	Иначе
		
		Секция.Параметры.Сумма = ?(Документ.СуммаДокумента = 0, "", Формат(Документ.СуммаДокумента, "ЧЦ=19; ЧДЦ=2") + " " + Строка(Документ.ВалютаДокумента));
				
	КонецЕсли; 
	
	Если ВывестиОрганизацию Тогда
		Секция.Параметры.Контрагент = Документ.Организация;
		Секция.Параметры.КонтрагентНаименование = ?(ПустаяСтрока(Документ.ОрганизацияНаименование), СокрЛП(Документ.Организация), СокрЛП(Документ.ОрганизацияНаименование));
	КонецЕсли; 
		
	Секция.Параметры.ПредставлениеОснования = ПредставлениеОснования;
	
	Секция.Параметры.ДатаНомер = Формат(Документ.Дата, "ДФ=dd.MM.yyyy") + ", № " + ОбщегоНазначения.ПолучитьНомерНаПечать(Документ, МассивПрефиксовДляРИБиОрганизации);
	
	Возврат Истина;
	
КонецФункции

Функция ПолучитьПредставлениеПоТипу(ТипЗначения)

	Представление =  СоответствиеТиповИПредставлений[ТипЗначения];
	Если Представление = Неопределено Тогда
		Если ТипЗначения <> ТипЗнч(Неопределено) И Документы.ТипВсеСсылки().СодержитТип(ТипЗначения) Тогда
			Представление = Метаданные.НайтиПоТипу(ТипЗначения).Представление();
		Иначе
			Представление = "";	
		КонецЕсли; 
		
		СоответствиеТиповИПредставлений.Вставить(ТипЗначения, Представление);
	КонецЕсли; 
	
	Возврат Представление; 

КонецФункции // ()

// Функция вызывается из тела процедуры "Сформировать".
// Функция осуществляет первичную обработку результатов запроса к движениям регистра НДСПродажи,
// и по данным запроса заполняет таблицу значений, на основании которой, будет напечатана книга продаж
// Параметры:
// 	Результат - ссылка на результаты выполнения запроса к данным регистра "НДСПродажи"
//  МоментОпределенияНалоговойБазыНДС
Функция ПодготовитьОтчетКВыводуНаПечать()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	РеестрСчетовФактур.Организация КАК Организация,
	|	РеестрСчетовФактур.СчетФактура,
	|	РеестрСчетовФактур.Дата КАК Дата,
	|	РеестрСчетовФактур.Номер КАК Номер,
	|	РеестрСчетовФактур.Контрагент КАК Контрагент,
	|	РеестрСчетовФактур.СуммаДокумента,
	|	РеестрСчетовФактур.ВалютаДокумента,
	|	РеестрСчетовФактур.ДокументыОснования.(
	|		ДокументОснование,
	|		ДокументОснование.Дата,
	|		ДокументОснование.Номер
	|	),
	|	ВЫБОР
	|		КОГДА РеестрСчетовФактур.Контрагент ЕСТЬ NULL 
	|				ИЛИ РеестрСчетовФактур.СуммаДокумента ЕСТЬ NULL 
	|				ИЛИ РеестрСчетовФактур.ВалютаДокумента ЕСТЬ NULL 
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ОпределитьПараметрыСчетаФактуры,
	|	ВЫБОР
	|		КОГДА ПОДСТРОКА(РеестрСчетовФактур.Контрагент.НаименованиеПолное, 1, 250) = """"
	|			ТОГДА РеестрСчетовФактур.Контрагент.Наименование
	|		ИНАЧЕ ПОДСТРОКА(РеестрСчетовФактур.Контрагент.НаименованиеПолное, 1, 250)
	|	КОНЕЦ КАК КонтрагентНаименование,
	|	ВЫБОР
	|		КОГДА ПОДСТРОКА(РеестрСчетовФактур.Организация.НаименованиеПолное, 1, 250) = """"
	|			ТОГДА РеестрСчетовФактур.Организация.Наименование
	|		ИНАЧЕ ПОДСТРОКА(РеестрСчетовФактур.Организация.НаименованиеПолное, 1, 250)
	|	КОНЕЦ КАК ОрганизацияНаименование
	|ИЗ
	|	(ВЫБРАТЬ
	|		СчетФактураВыданный.Ссылка КАК СчетФактура,
	|		СчетФактураВыданный.Дата КАК Дата,
	|		СчетФактураВыданный.Номер КАК Номер,
	|		СчетФактураВыданный.Организация КАК Организация,
	|		СчетФактураВыданный.ДокументОснование КАК ДокументОснование,
	|		СчетФактураВыданный.Контрагент КАК Контрагент,
	|		СчетФактураВыданный.СуммаДокумента КАК СуммаДокумента,
	|		СчетФактураВыданный.ВалютаДокумента КАК ВалютаДокумента,
	|		СчетФактураВыданный.ДокументыОснования.(
	|			Ссылка КАК Ссылка,
	|			ДокументОснование КАК ДокументОснование
	|		) КАК ДокументыОснования
	|	ИЗ
	|		Документ.СчетФактураВыданный КАК СчетФактураВыданный
	|	ГДЕ
	|		(НЕ СчетФактураВыданный.ПометкаУдаления)
	|		И СчетФактураВыданный.Организация = &Организация
	|		И (&ОтображатьСФНаАванс
	|				ИЛИ ((НЕ СчетФактураВыданный.НаАванс)
	|					ИЛИ СчетФактураВыданный.Дата >= ДАТАВРЕМЯ(2009, 1, 1)))
	|		И (Не &ОтображатьТолькоСФНаАванс
	|				ИЛИ СчетФактураВыданный.НаАванс)
	|		И СчетФактураВыданный.Дата >= &НачалоПериода
	|		И СчетФактураВыданный.Дата <= &КонецПериода) КАК РеестрСчетовФактур
	|ГДЕ
	|	ВЫБОР
	|			КОГДА &ОтбиратьПоКонтрагенту
	|				ТОГДА РеестрСчетовФактур.Контрагент В ИЕРАРХИИ (&Контрагент)
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата,
	|	Номер";
	
	Если не СформироватьОтчетПоСтандартнойФорме и ГруппироватьПоКонтрагентам Тогда
		Запрос.Текст = Запрос.Текст + "
		|ИТОГИ ПО Контрагент";
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "УПОРЯДОЧИТЬ ПО", "УПОРЯДОЧИТЬ ПО
		|КонтрагентНаименование, ");
	КонецЕсли; 	
	
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", КонецДня(КонецПериода));
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ОтображатьСФНаАванс", Не СформироватьОтчетПоСтандартнойФорме и ОтображатьСФНаАванс);
	Запрос.УстановитьПараметр("ОтображатьТолькоСФНаАванс", Не СформироватьОтчетПоСтандартнойФорме и ОтображатьТолькоСФНаАванс);
	Запрос.УстановитьПараметр("ОтбиратьПоКонтрагенту", Не СформироватьОтчетПоСтандартнойФорме И ОтбиратьПоКонтрагенту И Не КонтрагентДляОтбора = Справочники.Контрагенты.ПустаяСсылка());
	Запрос.УстановитьПараметр("Контрагент", КонтрагентДляОтбора);
	Запрос.УстановитьПараметр("ВалютаРегламентированногоУчета", Константы.ВалютаРегламентированногоУчета.Получить());

	Возврат Запрос.Выполнить();

КонецФункции

#КонецЕсли

СоответствиеТиповИПредставлений = Новый Соответствие();
ТипыДокументов = Документы.ТипВсеСсылки();