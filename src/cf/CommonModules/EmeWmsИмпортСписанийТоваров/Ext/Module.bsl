// Copyright (C) 2012-2016 Engineer Mareev Enterprises

//	Процедура ИмпортироватьСписанияТоваров импортирует списания товаров.
Процедура ИмпортироватьСписанияТоваров(ERPData) Экспорт
	
	ЗаголовкиСообщений = Новый ТаблицаЗначений();
	EmeWmsERPEngine.GetHeaders(ERPData, "wms", "erp", "offwrite", "NEW,WRN", ЗаголовкиСообщений);
	
	Счетчик = 0;
	Для Каждого ЗаголовокСообщения Из ЗаголовкиСообщений Цикл
		
		//	ВАЖНО! Транзакция источника сообщения должна быть внешней	
		EmeWmsERPEngine.BeginImport(ERPData, "wms", "erp", "offwrite", ЗаголовокСообщения.id);
		Попытка
			НовоеСообщение = (ЗаголовокСообщения.state = "NEW");
			Трассировка = "";
			ТемаСообщения = "";
			НачатьТранзакцию();
			Попытка
				Пока EmeWmsERPEngine.NextHeaderLine(ERPData) <> 0 Цикл
					ИмпортироватьСписаниеТоваров(ERPData, Трассировка, ТемаСообщения);
					Счетчик = Счетчик + 1;
				КонецЦикла;
				ЗафиксироватьТранзакцию();
			Исключение
				ОтменитьТранзакцию();
				ВызватьИсключение(ОписаниеОшибки());
			КонецПопытки;
			EmeWmsERPEngine.CommitImport(ERPData);
			Если НовоеСообщение И ТемаСообщения <> "" Тогда
				EmeWmsУтилиты.СообщитьПоПочте(
					ТемаСообщения,
					"ИБ: " +  СтрокаСоединенияИнформационнойБазы() + Символы.ПС +
					"ID: " + ЗаголовокСообщения.id + Символы.ПС +
					Трассировка);
			КонецЕсли
		Исключение
			EmeWmsERPEngine.RollbackImport(ERPData);
			Ошибка = ОписаниеОшибки();
			Сообщить(Ошибка);
			ЗаписьЖурналаРегистрации("Импорт из EME.WMS",,,,"Импорт сообщений OFFWRITE: " + Ошибка);
			EmeWmsУтилиты.СообщитьПоПочте(
				"Критическая ошибка импорта сообщения OFFWRITE",
				"ИБ: " +  СтрокаСоединенияИнформационнойБазы() + Символы.ПС +
				"ID: " + ЗаголовокСообщения.id + Символы.ПС +
				Ошибка);
		КонецПопытки;
		
	КонецЦикла;
	
	Если Счетчик <> 0 Тогда
		Сообщить("Проимпортировали сообщения OFFWRITE (" + Счетчик + "шт)");
	КонецЕсли
	
КонецПроцедуры

Процедура ИмпортироватьСписаниеТоваров(ERPData, Трассировка, ТемаСообщения)
	
	//*** Делаем проверки ***

	НомерEME = ERPData.header.id;
	
	Если Не EmeWmsУтилиты.ПолучитьСписаниеТоваров(НомерEME).Пустая() Тогда
		EmeWmsERPEngine.Success(ERPData);		
		Возврат
	КонецЕсли;
	
	СкладСсылка = Справочники.Склады.НайтиПоРеквизиту("EmeWmsКод", ERPData.header.whs_code);
	Если СкладСсылка.Пустая() Тогда
		EmeWmsERPEngine.ErrorHeader(ERPData, "WHSBAD");
		Возврат
	КонецЕсли;
		
	ОрганизацияСсылка = EmeWmsУтилиты.ПолучитьСсылкуНаОрганизацию();
	Если ОрганизацияСсылка.Пустая() Тогда
		EmeWmsERPEngine.ErrorHeader(ERPData, "VNDBAD");
		Возврат
	КонецЕсли;
	
	//	Соберем товары и количества в карту соответствий (ключ - ссылка на товар, значение - количество)
	КоличестваТоваров = Новый Соответствие();
	EmeWmsERPEngine.SelectChild(ERPData, "lines");
	EmeWmsУтилиты.ПолучитьКоличестваТоваров(ERPData, КоличестваТоваров, "quantity", Ложь);
	
	//	Если были ошибки в номенклатуре - выйдем
	Если EmeWmsERPEngine.HasErrors(ERPData) Тогда
		Возврат
	КонецЕсли;
							
	//*** Проверки сделали, пишем в базу данных 1C ***
	
	Списание = Документы.СписаниеТоваров.СоздатьДокумент();
	Списание.Дата = ТекущаяДата();
	Списание.Склад = СкладСсылка;
	Списание.Организация = ОрганизацияСсылка;
	Списание.ОтражатьВУправленческомУчете = Истина;
	Списание.ОтражатьВБухгалтерскомУчете = Истина;
	Списание.ОтражатьВНалоговомУчете = Истина;
	Списание.Комментарий = "Создан автоматом на основании документа списания EME: " +
		НомерEME + " (" + ERPData.header.comment + ")";
	Списание.EmeWmsНомер = НомерEME;
	Списание.EmeWmsДатаИмпорта = ТекущаяДата();
	
	СоздатьТабличнуюЧасть(Списание, КоличестваТоваров);
	
	Списание.Записать(РежимЗаписиДокумента.Запись);
	Трассировка = Трассировка + Списание + Символы.ПС;
	ТемаСообщения = "Необходимо провести документ по списанию EME " + НомерEME;
		
	EmeWmsERPEngine.Success(ERPData);
		
КонецПроцедуры

Процедура СоздатьТабличнуюЧасть(Списание, КоличестваТоваров) Экспорт
	
	//	Добавим недостающие товары
	Для Каждого КоличествоТовара Из КоличестваТоваров Цикл
		
		СтрокаСписание 					= Списание.Товары.Добавить();
		СтрокаСписание.Номенклатура 	= КоличествоТовара.Ключ;
		СтрокаСписание.Количество 		= КоличествоТовара.Значение;
		СтрокаСписание.Коэффициент = 1;
		СтрокаСписание.ЕдиницаИзмерения = EmeWmsУтилиты.ПолучитьЕдиницуИзмерения(
			СтрокаСписание.Номенклатура,
			СтрокаСписание.Номенклатура.БазоваяЕдиницаИзмерения);
		
	КонецЦикла;
	
КонецПроцедуры
