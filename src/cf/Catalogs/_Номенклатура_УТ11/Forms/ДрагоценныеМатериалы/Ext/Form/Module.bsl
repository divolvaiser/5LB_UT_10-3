
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	 УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ЗначениеВРеквизитФормы(Параметры.Номенклатура.ПолучитьОбъект(), "Номенклатура");
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	ОчиститьСообщения();
	
	ПроверитьЗаписатьНаСервере(Отказ);
	
	Если Не Отказ Тогда
		Оповестить("Запись_ДрагоценныеМатериалы", Номенклатура.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПроверитьЗаписатьНаСервере(Отказ)
	
	СправочникОбъект = ДанныеФормыВЗначение(Номенклатура, Тип("СправочникОбъект.Номенклатура"));
	
	Для Каждого СтрокаДрагоценныйМатериал Из СправочникОбъект.ДрагоценныеМатериалы Цикл
		Если Не ЗначениеЗаполнено(СтрокаДрагоценныйМатериал.ДрагоценныйМатериал) Тогда
			
			ТекстСообщения = НСтр("ru = 'Не заполнена колонка ""%ИмяКолонки%"" в строке ""%НомерСтроки%"" списка ""Драгоценные металлы и камни""'");
			
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ИмяКолонки%",
				Метаданные.Справочники.Номенклатура.ТабличныеЧасти.ДрагоценныеМатериалы.Реквизиты.ДрагоценныйМатериал.Синоним);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%НомерСтроки%",
				СтрокаДрагоценныйМатериал.НомерСтроки);
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,
				,
				"Номенклатура.ДрагоценныеМатериалы["+(СтрокаДрагоценныйМатериал.НомерСтроки-1)+"].ДрагоценныйМатериал",
				,
				Отказ);
				
		КонецЕсли;
		Если Не ЗначениеЗаполнено(СтрокаДрагоценныйМатериал.Количество) Тогда
			
			ТекстСообщения = НСтр("ru = 'Не заполнена колонка ""%ИмяКолонки%"" в строке ""%НомерСтроки%"" списка ""Драгоценные металлы и камни""'");
			
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ИмяКолонки%",
				Метаданные.Справочники.Номенклатура.ТабличныеЧасти.ДрагоценныеМатериалы.Реквизиты.Количество.Синоним);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%НомерСтроки%",
				СтрокаДрагоценныйМатериал.НомерСтроки);

			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,
				,
				"Номенклатура.ДрагоценныеМатериалы["+(СтрокаДрагоценныйМатериал.НомерСтроки-1)+"].Количество",
				,
				Отказ);
			
		КонецЕсли;
		Если Не ЗначениеЗаполнено(СтрокаДрагоценныйМатериал.Расположение) 
			И СтрокаДрагоценныйМатериал.ДрагоценныйМатериал = ПредопределенноеЗначение("Справочник.ДрагоценныеМатериалы.Алмазы") Тогда
			
			ТекстСообщения = НСтр("ru = 'Не заполнена колонка ""%ИмяКолонки%"" в строке ""%НомерСтроки%"" списка ""Драгоценные металлы и камни""'");
			
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ИмяКолонки%",
				Метаданные.Справочники.Номенклатура.ТабличныеЧасти.ДрагоценныеМатериалы.Реквизиты.Расположение.Синоним);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%НомерСтроки%",
				СтрокаДрагоценныйМатериал.НомерСтроки);

			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,
				,
				"Номенклатура.ДрагоценныеМатериалы["+(СтрокаДрагоценныйМатериал.НомерСтроки-1)+"].Расположение",
				,
				Отказ);
			
		КонецЕсли;
	КонецЦикла;
	
	Если Не Отказ Тогда
		СправочникОбъект.Записать();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
КонецПроцедуры

#КонецОбласти
