//27.01.2017, Юра
Функция СоздатьПрокси(Наименование) Экспорт
	УстановитьПривилегированныйРежим(Истина);//15.02.2017, Юра
	
	ЗапретИспользованияРабочихWebСервисовВКопиях(Наименование);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	_5LB_ОписанияWebСервисов.ИмяWSссылки,
		|	_5LB_ОписанияWebСервисов.URLисточника,
		|	_5LB_ОписанияWebСервисов.ИмяWebСервиса,
		|	_5LB_ОписанияWebСервисов.URIПростанстваИменWebСервиса,
		|	_5LB_ОписанияWebСервисов.ИмяПользователя,
		|	_5LB_ОписанияWebСервисов.Пароль
		|ИЗ
		|	РегистрСведений._5LB_ОписанияWebСервисов КАК _5LB_ОписанияWebСервисов
		|ГДЕ
		|	_5LB_ОписанияWebСервисов.Наименование = &Наименование";
	Запрос.УстановитьПараметр("Наименование", Наименование);
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		ИмяWSссылки = ВыборкаДетальныеЗаписи.ИмяWSссылки;
		URLисточника = ВыборкаДетальныеЗаписи.URLисточника;
		ИмяWebСервиса = ВыборкаДетальныеЗаписи.ИмяWebСервиса;
		URIПростанстваИменWebСервиса = ВыборкаДетальныеЗаписи.URIПростанстваИменWebСервиса;
		ИмяПользователя = ВыборкаДетальныеЗаписи.ИмяПользователя;
		Пароль = ВыборкаДетальныеЗаписи.Пароль;
	Иначе
		Сообщить("Не могу в регистре сведений ""Описания Web сервисов"" найти описание для "+Наименование+".");
		Возврат Неопределено;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИмяWSссылки) Тогда
		Прокси = WSСсылки[ИмяWSссылки].СоздатьWSПрокси(URIПростанстваИменWebСервиса,ИмяWebСервиса, ИмяWebСервиса+"Soap");
	ИначеЕсли ЗначениеЗаполнено(URLисточника) Тогда //Вариант без добавления WS-ссылки в конфигурацию. Он медленей, но не требует изменения конфигурации при изменении сервиса.
		Определения = новый WSОпределения(URLисточника,ИмяПользователя,Пароль);
		Прокси = новый WSПрокси(Определения,URIПростанстваИменWebСервиса,ИмяWebСервиса,ИмяWebСервиса+"Soap");
	Иначе
		Сообщить("В регистре сведений ""Описания Web сервисов"" для описания "+Наименование+" не указаны ""ИмяWSссылки"" и ""URLисточника"".");
		Возврат Неопределено;
	КонецЕсли;
	Прокси.Пользователь = ИмяПользователя; 
	Прокси.Пароль = Пароль;
	
	Возврат Прокси;
	
КонецФункции

//14.02.2017, Юра
//Для копий баз запрещаем обращение к рабочим сервисам
Процедура ЗапретИспользованияРабочихWebСервисовВКопиях(Наименование)
	Если НЕ ВРег(СтрокаСоединенияИнформационнойБазы()) = ВРег("Srvr=""1CSERV"";Ref=""C81_UT"";") Тогда //Это не ЦБ УТ10.
		НаборЗаписей = РегистрыСведений._5LB_ОписанияWebСервисов.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Наименование.Установить(Наименование);
		НаборЗаписей.Прочитать();
		Если НаборЗаписей.Количество() = 1 Тогда
			ТекЗапись = НаборЗаписей[0];
			//удалить в существующих записях регистра данные о предопределенных ИмяWSссылки и/или URLисточника:
			ТекЗапись.ИмяWSссылки = ""; //Даже проверять не будем, в тестовых базах всегда запрещаем использовать WSссылки
			
			НовыйURLИсточника = ТекЗапись.URLИсточника;
			Для Каждого Строка Из Метаданные.WSСсылки Цикл
				Если ЗначениеЗаполнено(НовыйURLИсточника) И ТекЗапись.URLИсточника = Строка.URLИсточника Тогда
					НовыйURLИсточника = "";
				КонецЕсли;
			КонецЦикла;
			ТекЗапись.URLИсточника = НовыйURLИсточника;
		ИначеЕсли НаборЗаписей.Количество() = 0 Тогда
			ТекЗапись = НаборЗаписей.Добавить();
			ТекЗапись.Наименование = Наименование;
		КонецЕсли;
		НаборЗаписей.Записать();
	КонецЕсли;
КонецПроцедуры
